# Contributor: cyanea-bt https://github.com/cyanea-bt

_realname=varint
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2018.4.7
pkgrel=1
pkgdesc="A basic varint implementation in python (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
_repository='fmoo/python-varint'
url="https://github.com/${_repository}"
msys2_references=(
  'pypi: varint'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
_commit='ed8484cfc0f24f794bda584ec3d1e8983b1db713'
source=("${url}/archive/${_commit}.tar.gz")
sha256sums=('SKIP')

pkgver() {
  # fetch date from github api, ref: https://stackoverflow.com/a/28973585
  api_response=$(curl -s "https://api.github.com/repos/${_repository}/commits/${_commit}")
  date_api=$(jq -br '.commit.committer.date' <<<"${api_response}")
  date_commit=$(date -d "${date_api}" "+%Y.%-m.%-d")
  echo "${date_commit}"
}

prepare() {
  rm -rf python-build-${MSYSTEM} || true  
  # cp -r "${_realname}-${_commit}" "python-build-${MSYSTEM}"
  mv "python-${_realname}-${_commit}" "python-build-${MSYSTEM}"
  rm "${_commit}.tar.gz"
}

build() {
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  cd "${srcdir}/python-build-${MSYSTEM}"

  # update version string in setup.py
  # ref: https://stackoverflow.com/a/11145362, https://stackoverflow.com/a/8822213
  sedcmd='s/^VERSION = .*/VERSION = "'${pkgver}'"/'
  sed -i "${sedcmd}" setup.py

  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
