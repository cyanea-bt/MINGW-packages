# Contributor: cyanea-bt https://github.com/cyanea-bt

_realname=lzfse
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.0
pkgrel=1
pkgdesc="The LZFSE compression library and command line tool"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64' 'clangarm64')
url="https://github.com/lzfse/lzfse"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
source=("https://github.com/lzfse/lzfse/archive/lzfse-${pkgver}.tar.gz") 
sha256sums=('cf85f373f09e9177c0b21dbfbb427efaedc02d035d2aade65eb58a3cbf9ad267')

prepare() {
    cd ${srcdir}
    mv ${_realname}-${_realname}-${pkgver} ${_realname}-${pkgver}
}

build() {
    declare -a extra_config
    if check_option "debug" "n"; then
        extra_config+=("-DCMAKE_BUILD_TYPE=Release")
    else
        extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
    fi

    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
        "${extra_config[@]}" \
        -DBUILD_SHARED_LIBS=ON \
        -DLZFSE_BUNDLE_MODE=OFF \
        -S "${_realname}-${pkgver}" \
        -B "build-${MSYSTEM}" \

    "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}"
}

check() {
    "${MINGW_PREFIX}"/bin/cmake.exe --build "build-${MSYSTEM}" --target test
}

package() {
    DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install "build-${MSYSTEM}"
    install -d "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
    install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}